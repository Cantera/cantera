from buildutils import *

Import('env', 'buildTargets', 'installTargets', 'libraryTargets')

localenv = env.Clone()
localenv.Append(CPPPATH=['#include', '#src'])

f90_src = mglob(localenv, '.', 'f90', 'cpp')

artifacts = localenv.Object(f90_src,
                            F90PATH=['#build/src/fortran'])
mods = [o for o in artifacts if o.path.endswith('.mod')]
objects = [o for o in artifacts if not o.path.endswith('.mod')]

lib = localenv.Library(target=pjoin('..','..','lib','cantera_fortran'),
                       source=objects)
buildTargets.extend(lib)

if localenv['addInstallTargets']:
    inst = localenv.Install('$inst_libdir', lib)
    installTargets.extend(inst)
    installTargets.extend(localenv.Install('$inst_incdir', mods))
