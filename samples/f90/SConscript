from os.path import join as pjoin, relpath
from buildutils import *

Import('env', 'install', 'buildSample')
localenv = env.Clone()

localenv['mak_stdlib'] = ['-l' + lib for lib in env['cxx_stdlib']]

# (program name, [source files])
samples = [('demo', ['demo.f90'])]

for programName, sources in samples:
    buildSample(localenv.Program, programName, sources,
                F90PATH='#build/src/fortran',
                LIBS=['cantera_fortran']+env['cantera_libs']+env['cxx_stdlib'],
                LIBPATH=[env['sundials_libdir'], env['blas_lapack_dir'],
                         env['extra_lib_dirs'], '#build/lib'],
                LINK='$FORTRAN_LINK')

    # Generate SConstruct files to be installed
    incdirs = [pjoin(localenv["ct_incroot"], "cantera")] # path to fortran .mod
    libdirs = [localenv["ct_libdir"]]
    if not localenv["package_build"]:
        incdirs.extend([localenv["sundials_include"], localenv["boost_inc_dir"]])
        incdirs.extend(localenv["extra_inc_dirs"])
        incdirs = list(set(incdirs))
        libdirs.extend([localenv["sundials_libdir"], localenv["blas_lapack_dir"]])
        libdirs.extend(localenv["extra_lib_dirs"])
        libdirs = list(set(libdirs))

    libs = ['cantera_fortran'] + localenv['cantera_libs'] + env['cxx_stdlib']
    linkflags = ("-g", localenv["thread_flags"], f"-Wl,-rpath,{localenv['ct_libdir']}")

    localenv['tmpl_cantera_incdirs'] = repr([x for x in incdirs if x])
    localenv['tmpl_cantera_libs'] = repr(libs)
    localenv['tmpl_cantera_libdirs'] = repr([x for x in libdirs if x])
    localenv['tmpl_cantera_linkflags'] = repr([x for x in linkflags if x])
    localenv['tmpl_cantera_frameworks'] = repr(localenv['FRAMEWORKS'])

    localenv['tmpl_progname'] = programName
    localenv['tmpl_sourcename'] = programName + '.f90'

    sconstruct = localenv.SubstFile('SConstruct', 'SConstruct.in')
    install("$inst_sampledir/f90", sconstruct)

    # Generate CMakeLists.txt to be installed
    localenv['cmake_cantera_incdirs'] = ' '.join(quoted(x) for x in incdirs if x)
    localenv['cmake_cantera_libs'] = ' '.join(libs)
    localenv['cmake_cantera_libdirs'] = ' '.join(quoted(x) for x in libdirs if x)
    cmakelists = localenv.SubstFile('CMakeLists.txt', 'CMakeLists.txt.in')
    install("$inst_sampledir/f90", cmakelists)

    if localenv["OS"] != "Windows":
        # Generate Makefile to be installed
        mak_path = pjoin(localenv["ct_incroot"], "cantera", "Cantera.mak")
        if " " in mak_path:
            # There is no reasonable way to handle spaces in Makefile 'include'
            # statement, so we fall back to using the relative path instead
            mak_path = relpath(mak_path, pjoin(localenv["ct_sampledir"], "f90"))
        localenv["make_Cantera_dot_mak"] = mak_path

        localenv["make_target"] = programName
        localenv["make_sourcefile"] = programName + ".f90"
        makefile = localenv.SubstFile("Makefile", "Makefile.in")
        install("$inst_sampledir/f90", makefile)
